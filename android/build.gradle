allprojects {
    repositories {
        google()
        mavenCentral()
    }
}

// AGP 8+ 필수: namespace 미지정 플러그인에 AndroidManifest package로 namespace 설정
subprojects { subproject ->
    subproject.afterEvaluate {
        if (subproject.plugins.hasPlugin("com.android.library")) {
            def android = subproject.extensions.findByName("android")
            if (android != null && android.namespace == null) {
                def manifest = file("${subproject.projectDir}/src/main/AndroidManifest.xml")
                if (manifest.exists()) {
                    def matcher = (manifest.text =~ /package="([^"]+)"/)
                    if (matcher.find()) {
                        android.namespace = matcher.group(1)
                    }
                }
            }
        }
    }
}

rootProject.buildDir = '../build'
subprojects {
    project.buildDir = "${rootProject.buildDir}/${project.name}"
}
subprojects {
    project.evaluationDependsOn(':app')
}

tasks.register("clean", Delete) {
    delete rootProject.buildDir
}
